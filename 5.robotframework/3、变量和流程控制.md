### 一、变量

#### 1、变量的类型

- ${变量名}---存储一个值

- @{变量名}---列表，存储多个值

- &{变量名}---字典

#### 2、变量的赋值

- 关键字创建创建并赋值

  **Set Variable**：设置一个变量

  **Create List**：创建一个列表

  **Create Dictionary**：创建一个字典

  ```python
  *** Test Cases ***
  第五个用例
      ${hi}    Set Variable    Hello,world!
      @{list}    Create List    a    b    c
      ${list}    Create List    a    b    c
      &{dict}    Create Dictionary    a=b    c=d
  ```

  ```
  # Message Log打印结果
  INFO : ${hi} = Hello,world!
  INFO : @{list} = [ a | b | c ]
  INFO : ${list} = ['a', 'b', 'c']
  INFO : &{dict} = { a=b | c=d }
  ```

  注：注意变量的作用域，定义在某个用例的变量只能在该用例中使用

- <font color="red">`*** Variables ***`</font>定义变量：当前文件全局有效

  ```python
  *** Variables ***
  ${say}    hello,robotframework
  @{list_1}    22    33    44    55    66    77
  &{dict_1}    today=Monday   
  
  *** Test Cases ***
  第六个用例
      Log    ${list_1}
      Log    ${list_1[1]}    # list下标索引取值
      Log    ${dict_1.today}  # dict.key获取value
  ```

  注：使用定义列表、字典的变量是，前置符号需要改过来。

- py文件中获取变量

  ```python
  # vars.py
  my_str = 'hello,python'
  my_list = [1,2,3,4,5,6]
  my_dict = {'hello':'world'}
  ```

  ```python
  *** Settings ***
  Variables    vars.py	# 引入变量文件
  
  *** Test Cases ***
  第七个用例
      Log    ${my_str}
      Log    ${my_list[2]}
      Log    ${my_dict['hello']}	# dict[key]获取value
  ```

  注：py文件列表和字典变量名加前缀：`LIST__变量名`、`DICT__变量名`，可使用robot的语法获取变量

### 二、异常处理

<font color='red'>Run Keyword And Return Status</font>关键字处理异常

```python
*** Test Cases ***
第八个用例
    ${num}    Set Variable    3.14
    ${passed}    Run Keyword And Return Status    Evaluate    4<${num}<10
    Run Keyword If    ${passed}    Log    passed为True    
    ...    ELSE    Log    passed为False
```

### 三、条件判断

<font color='red'>Run Keyword If</font>关键字判断分支

```python
*** Test Cases ***
第九个用例
    ${num}    Set Variable    3.14
    Run Keyword If    ${num}>20    Log    变量num大于20
    ...    ELSE IF    ${num}>10    Log    变量num大于10小于20
    ...    ELSE IF    ${num}>1    Log    变量大于1小于10 
    ...    ELSE    Log    变量小于等于1
    ${str}    Set Variable    hello
    Run Keyword If    '${str}'=='hello'    Log    字符串等于hello
    ...    ELSE    Log    字符串不等于hello 
```

注：**字符串比较需要给变量加引号**

### 四、循环

#### 1、普通循环:FOR...IN...END

```python
*** Test Cases ***
第十个用例
    FOR    ${item}    In    @{list_1}
        Log    ${item}
        Exit For Loop If    ${item}==55   # 条件成立退出循环 
    END
```

#### 2、FOR...IN RANGE...END循环

```python
*** Test Cases ***
第十一个用例
	${length}    Get Length    ${list_1}
    FOR    ${index}    IN RANGE    ${length}
        Log    ${list_1[${index}]}    
    END
```

#### 3、嵌套循环：把内层循环定义成关键字

```python
*** Keywords ***
内层循环
    ${length}    Get Length    ${list_1}
    FOR    ${index}    IN RANGE    ${length}
        Log    ${list_1[${index}]}       
    END
    
*** Test Cases ***
第十二个用例
    ${length}    Get Length    ${list_1}
    FOR    ${index}    IN RANGE    ${length}
        内层循环    
    END
```







